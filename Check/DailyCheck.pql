program
. string * 2 HOUR$
. string * 100 EMAIL$ CC$ SERVER$ REPORT$
. integer * 1 YES NO DB# OK# USERS#
. integer * 2 FILES# FILE#
. integer * 4 STAT#
. string * 200 OUT$ EXEC$ HOURLY$
. HOUR$ = timec(now(0),'HH')
. OUT$ = '\\Dccmaincsfs\Data1\DCC\BackupRoutine\Check\Logs\' + datec(today(0),'YYYY_MM_DD ') + timec(now(0),'HH.MM.SS') + '.txt'
. HOURLY$ = '\\Dccmaincsfs\Data1\DCC\BackupRoutine\Check\Hourly\'
. ifthen (HOUR$ = '06')
.  FILES# = filecnt(HOURLY$ + '*.txt')
.  for FILE# = 1,FILES#
.   FILE$ = filen(HOURLY$ + '*.txt',FILE#)
.   t = delfile(HOURLY$ + FILE$)
.  end for
. endif
. set NO DB# STAT# (0) YES (1)
. include file '\\Dccmaincsfs\Data1\DCC\BackupRoutine\Check\DBSLIST.txt'
. open (OUT$) dsnvar=OUT$ write lrecl=300
.  write (OUT$) [datec(today(0),'MM/DD/YYYY')] 12t [timec(now(0),'HH:MM:SS')]
.  for DB# = 1,DB#C
.   if (STA$(DB#) <> 'Live') next for
.   EXEC$ = 'set master ' + MST$(DB#)
.   execute dbms [EXEC$]
.   pql connect database DB$N(DB#)
                prefix   DIR$(DB#)
                iostat   STAT#
.   ifthen (STAT# < 0)
.    write (OUT$) DB$N(DB#) 10t ['****Cannot connect to ' + DB$N(DB#) + ' !!!!']
.    EXEC$ = '\\192.168.53.236\SIRXS\Email\blat -to merleda@nyspi.columbia.edu' +
             ' -subject "Connection Error"' +
             ' -body "' + DB$N(DB#) + ' did not connect."'
.    pql escape EXEC$
.   elseif (exists(MST$(DB#)) = YES)
.    USERS# = system(68) - 1
.    ifthen (USERS# = 1)
.     write (OUT$) DB$N(DB#) 10t USERS# 'current user'
.    elseif (USERS# > 1)
.     write (OUT$) DB$N(DB#) 10t USERS# 'current users'
.    elseif (USERS# = 0)
.     write (OUT$) DB$N(DB#)
.    elseif (USERS# < 0)
.     write (OUT$) DB$N(DB#) 10t ['****Cannot connect to ' + MST$(DB#) + ' !!!!']
.     EXEC$ = '\\192.168.53.236\SIRXS\Email\blat -to merleda@nyspi.columbia.edu' +
              ' -subject "Connection Error"' +
              ' -body "' + MST$(DB#) + ' did not connect."'
.     pql escape EXEC$
.    endif
.   else
.    write (OUT$) DB$N(DB#) 10t 'No master assigned'
.   endif
|
.   pql disconnect database DB$N(DB#)
.   EXEC$ = 'clear master ' + MST$(DB#)
.   execute dbms [EXEC$]
.  end for
. close (OUT$)
. ifthen (fileis(HOURLY$ + HOUR$ + '.txt') = NO)
.  EXEC$ = '\\192.168.53.236\SIRXS\Email\blat -to merleda@nyspi.columbia.edu' +
           ' -subject "All Is Well"' +
           ' -body "' + HOUR$ + ':00 and all is well."'
.  pql escape EXEC$
. endif
. HOURLY$ = HOURLY$ + HOUR$ + '.txt'
. open (HOURLY$) dsnvar=HOURLY$ write lrecl=300
. close (HOURLY$)
end program
